Attribute VB_Name = "SOUFX_F51"
Option Explicit
'
'スロット名      :FAX番号・画面項目スロット
'ユニット名      :SOUFX.F51
'記述者          :Standard Library
'作成日付        :2006/08/28
'使用プログラム  :SOUMT51
'
'更新日付        :2006/11/09
'更新内容        :エラーチェック追加

Function SOUFX_CheckC(ByVal SOUFX, ByVal De_Index)

    Dim Rtn         As Integer
    Dim CntHP       As Integer
    Dim LenAll      As Integer
    Dim lngI        As Long
    Dim lngPOS      As Long

    SOUFX_CheckC = 0
    
    LenAll = Len(Trim(SOUFX))
    
    If LenAll = 0 Then
        Exit Function
    End If
    
    '電話番号ハイフン先頭エラー
    If Left(SOUFX, 1) = "-" Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 0)                'ハイフンが先頭にあります。
        SOUFX_CheckC = -1
        Exit Function
    End If
    
    '電話番号ハイフン末尾エラー
    If Right(Trim(SOUFX), 1) = "-" Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 1)                'ハイフンが末尾にあります。
        SOUFX_CheckC = -1
        Exit Function
    End If
    
    '電話番号ハイフン連続入力エラー
    For lngI = 1 To Len(Trim(SOUFX))
        If Mid(Trim(SOUFX), lngI, 1) = "-" Then
            If Mid(Trim(SOUFX), lngI + 1, 1) = "-" Then
                Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 2)        'ハイフンを複数連続して入力しています。
                SOUFX_CheckC = -1
                Exit Function
            End If
        End If
    Next

    '総桁数チェック
    If LenAll > Len506 Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 3)            '桁数オーバーです。
        SOUFX_CheckC = -1
        Exit Function
    End If
    
    'ハイフン個数チェック
    lngPOS = 0
    CntHP = 0
    For lngI = 1 To LenAll
        If Mid(SOUFX, lngI, 1) = "-" Then
            CntHP = CntHP + 1
            If CntHP = Len507 Then
                lngPOS = lngI               '2個目の位置を退避
            End If
        End If
    Next
    If CntHP <> Len507 Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 4)            'ハイフン個数の誤りです。
        SOUFX_CheckC = -1
        Exit Function
    End If
    
    '電話番号下桁チェック
    If Len(Mid(Trim(SOUFX), lngPOS + 1, Len(Trim(SOUFX)) - lngPOS)) <> Len511 Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 5)            '入力が不正です。
        SOUFX_CheckC = -1
        Exit Function
    Else
        If IsNumeric(Mid(Trim(SOUFX), lngPOS + 1, Len(Trim(SOUFX)) - lngPOS)) = False Then
            Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 5)        '入力が不正です。
            SOUFX_CheckC = -1
            Exit Function
        End If
    End If
    
End Function

