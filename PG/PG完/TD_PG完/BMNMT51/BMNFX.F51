Attribute VB_Name = "BMNFX_F51"
Option Explicit
'
'スロット名      :FAX番号・画面項目スロット
'ユニット名      :BMNFX.F51
'記述者          :Standard Library
'作成日付        :2006/08/28
'使用プログラム  :BMNMT51
'
'更新日付        :2006/11/09
'更新内容        :エラーチェック追加

Function BMNFX_CheckC(ByVal BMNFX, ByVal De_Index)

    Dim Rtn         As Integer
    Dim CntHP       As Integer
    Dim LenAll      As Integer
    Dim lngI        As Long
    Dim lngPOS      As Long

    BMNFX_CheckC = 0
    
    LenAll = Len(Trim(BMNFX))
    
    If LenAll = 0 Then
        Exit Function
    End If
    
    '電話番号ハイフン先頭エラー
    If Left(BMNFX, 1) = "-" Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 0)                'ハイフンが先頭にあります。
        BMNFX_CheckC = -1
        Exit Function
    End If
    
    '電話番号ハイフン末尾エラー
    If Right(Trim(BMNFX), 1) = "-" Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 1)                'ハイフンが末尾にあります。
        BMNFX_CheckC = -1
        Exit Function
    End If
    
    '電話番号ハイフン連続入力エラー
    For lngI = 1 To Len(Trim(BMNFX))
        If Mid(Trim(BMNFX), lngI, 1) = "-" Then
            If Mid(Trim(BMNFX), lngI + 1, 1) = "-" Then
                Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 2)        'ハイフンを複数連続して入力しています。
                BMNFX_CheckC = -1
                Exit Function
            End If
        End If
    Next

    '総桁数チェック
    If LenAll > Len506 Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 3)            '桁数オーバーです。
        BMNFX_CheckC = -1
        Exit Function
    End If
    
    'ハイフン個数チェック
    lngPOS = 0
    CntHP = 0
    For lngI = 1 To LenAll
        If Mid(BMNFX, lngI, 1) = "-" Then
            CntHP = CntHP + 1
            If CntHP = Len507 Then
                lngPOS = lngI               '2個目の位置を退避
            End If
        End If
    Next
    If CntHP <> Len507 Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 4)            'ハイフン個数の誤りです。
        BMNFX_CheckC = -1
        Exit Function
    End If
    
    '電話番号下桁チェック
    If Len(Mid(Trim(BMNFX), lngPOS + 1, Len(Trim(BMNFX)) - lngPOS)) <> Len511 Then
        Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 5)            '入力が不正です。
        BMNFX_CheckC = -1
        Exit Function
    Else
        If IsNumeric(Mid(Trim(BMNFX), lngPOS + 1, Len(Trim(BMNFX)) - lngPOS)) = False Then
            Rtn = DSP_MsgBox(SSS_ERROR, "TEL_FAX_NO", 5)        '入力が不正です。
            BMNFX_CheckC = -1
            Exit Function
        End If
    End If
    
End Function

